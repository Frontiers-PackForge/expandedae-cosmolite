buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.moddev.legacyforge' version '2.0.91'
    id 'io.freefair.lombok' version '8.13.1'
}

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

legacyForge {
    version = project.minecraft_version + '-' + project.forge_version

    runs {
        configureEach {
            gameDirectory = project.file('run')
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
            sourceSet = sourceSets.main


            systemProperty 'mixin.excludes', 'architectury.mixins.json'
            jvmArgument "-mixin.config=mixins.${mod_id}.json"
            jvmArgument "-XX:+AllowEnhancedClassRedefinition"
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'forge.logging.console.level', 'debug'
            systemProperty 'forge.enabledGameTestNamespaces', 'expandedae'
        }

        server {
            server()
            sourceSet = sourceSets.main

            systemProperty 'forge.enabledGameTestNamespaces', mod_id
            programArguments.addAll('--nogui', '--world', 'world-extra')
            programArgument "-mixin.config=mixins.${mod_id}.json"
        }

        data {
            data()

            sourceSet = sourceSets.main

            programArguments.addAll('--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath())
            programArguments.addAll('--existing-mod', 'ae2')
        }
    }
    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}
sourceSets {
    main {
        ext.refMap = "mixins.${mod_id}.refmap.json"
    }
}

def extraModsDir = "extra-mods"
file(extraModsDir).mkdir()

repositories {
    mavenLocal()
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        url = uri("https://modmaven.dev/")
        content {
            includeGroup "appeng"
            includeGroup "de.mari_023"
        }
    }
    maven { // Registrate
        url "https://maven.tterrag.com/"
    }
    maven {
        name = 'GTCEu Maven'
        url = 'https://maven.gtceu.com'
        content {
            includeGroup 'com.gregtechceu.gtceu'
            includeGroup 'appeng'
        }
    }
    maven {
        name "firstdarkdev"
        url "https://maven.firstdarkdev.xyz/snapshots"
    }
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    flatDir {
        name "extra-mods"
        dir file(extraModsDir)
    }
}

dependencies {
    // literally AE2
    modImplementation "appeng:appliedenergistics2-forge:$ae2cl_version"
    modImplementation "curse.maven:guideme-1173950:6489464"

    // AE Addons
    modImplementation "curse.maven:applied-energistics-2-wireless-terminals-459929:5217955"
    modImplementation "curse.maven:mega-cells-622112:6175008"
    modImplementation "curse.maven:ex-pattern-provider-892005:6518515"
    // And their dependencies
    modImplementation "curse.maven:glodium-957920:5226922"
    modRuntimeOnly "curse.maven:architectury-api-419699:5137938"
    modRuntimeOnly "curse.maven:curios-309927:6418456"
    modRuntimeOnly "curse.maven:geckolib-388172:6027567"
    modRuntimeOnly "curse.maven:cloth-config-348521:5729105"

    modImplementation("com.gregtechceu.gtceu:gtceu-1.20.1:$gtceu_version") { transitive = false }
    modImplementation("com.lowdragmc.ldlib:ldlib-forge-1.20.1:$ldlib_version") { transitive = false }

    modRuntimeOnly "curse.maven:jade-324717:6271651"
    modRuntimeOnly "curse.maven:observable-509575:5643037"
    modRuntimeOnly "curse.maven:architectury-api-419699:5137938"
    modRuntimeOnly "curse.maven:kotlin-for-forge-351264:5402061"

    modRuntimeOnly "curse.maven:patchouli-306770:6164575"
    modImplementation "dev.emi:emi-forge:${emi_version}"
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

    for (extraModJar in fileTree(dir: "extra-mods", include: '*.jar')) {
        def basename = extraModJar.name.substring(0, extraModJar.name.length() - ".jar".length())
        def versionSep = basename.lastIndexOf('-')
        assert versionSep != -1
        def artifactId = basename.substring(0, versionSep)
        def version = basename.substring(versionSep + 1)
        modRuntimeOnly "extra-mods:$artifactId:$version"
    }

    compileOnly "org.jetbrains:annotations:26.0.2-1"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.4.1"))
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
                             mod_authors         : mod_authors, mod_description: mod_description,]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}


tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
tasks.withType(Jar).configureEach {
    destinationDirectory.set(file("$rootDir/jar"))
}
mixin {
    add sourceSets.main, "mixins.expandedae.refmap.json"
    config "mixins.expandedae.json"
}

jar {
    manifest {
        attributes([
                "MixinConfigs": "mixins.expandedae.json"
        ])
    }
}